plugins {
    id 'base'
    id 'java'
    id "me.modmuss50.mod-publish-plugin" version "0.8.4"
}

base {
    archivesName = project.file_name_paper
}
version = project.paper_version
group = project.maven_group

repositories {
    mavenCentral()
    maven {
        name = 'papermc'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

sourceSets {
    // Define a new sourceSet for the common code
    main {
        java {
            srcDir '../common/src/main/java'
        }
        resources {
            srcDir '../common/src/main/resources'
        }
    }
}

dependencies {
    implementation 'org.apache.commons:commons-configuration2:2.12.0'
    compileOnly ("io.papermc.paper:paper-api:${project.minecraft_version}-R0.1-SNAPSHOT")
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset = 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

//build in testing server dir
tasks.register('buildAndMoveJar', Jar) {
    archiveBaseName.set("directionhud-paper-dev")
    version = "0"
    from(sourceSets.main.output)

    destinationDirectory.set(file(env.fetchOrNull('LINUX_MOUNT')+"Minecraft/Servers/Paper/Latest/plugins"))
//    destinationDirectory.set(file("R:/Minecraft/Servers/Paper/Latest/plugins"))
}

publishMods {
    changelog = file('../changelog.md').text
    type = STABLE

    file = jar.archiveFile
    modLoaders.add("paper")
    modLoaders.add("folia")
    modLoaders.add("purpur")

    version = paper_version
    displayName = "v${paper_version} [Paper]"

    modrinth {
        accessToken = env.fetchOrNull('MODRINTH')
        projectId = "H8WCXRtb"
        minecraftVersions.addAll(minecraft_versions.split(",").toList())

        requires("fabric-api")

        announcementTitle = "Download from Modrinth"
    }
}