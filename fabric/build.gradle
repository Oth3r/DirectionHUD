plugins {
	id 'base'
	id 'fabric-loom' version '1.8-SNAPSHOT'
	id 'maven-publish'
	id "com.modrinth.minotaur" version "2.+"
	id 'net.darkhax.curseforgegradle' version '1.1.+'
}

base {
	archivesName = project.file_name_fabric
}
version = project.mod_version
group = project.maven_group

repositories {
	maven { url "https://maven.terraformersmc.com/releases/" }
	maven {
		name = "geyser"
		url = uri("https://repo.opencollab.dev/main/")
	}
}

sourceSets {
	main {
		java.srcDirs = ['src/main/java', '../common/src/main/java']
		resources.srcDirs = ['src/main/resources', '../common/src/main/resources']
	}
}
dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"

	implementation 'org.apache.commons:commons-text:1.12.0'
}

processResources {
	filteringCharset "UTF-8"

	var replaceProperties = [
			version                : project.version,
			minecraft_version      : minecraft_version,
			min_minecraft_version  : min_minecraft_version,
			loader_version         : loader_version
	]

	inputs.properties replaceProperties
	filesMatching("fabric.mod.json") {
		expand replaceProperties
	}
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}

import com.modrinth.minotaur.dependencies.ModDependency

modrinth {
	token = env.fetchOrNull('MODRINTH')
	projectId = 'H8WCXRtb'
	versionNumber = project.mod_version
	versionName = "v${project.mod_version}"
	uploadFile = remapJar
	gameVersions = [project.minecraft_version]
	loaders = ['fabric', 'quilt']
	dependencies = [
			new ModDependency('P7dR8mSH', 'required') // Creates a new required dependency on Fabric API
	]
	changelog = file('changelog.md').text
}

import net.darkhax.curseforgegradle.TaskPublishCurseForge

tasks.register('publishCurseForge', TaskPublishCurseForge) {
	apiToken = env.fetchOrNull('CURSEFORGE')

	def mainFile = upload(843483, remapJar)
	mainFile.changelog = file('changelog.md')
	mainFile.displayName = "v${project.mod_version}"
	mainFile.addModLoader("fabric", "quilt")
	mainFile.releaseType = "release"
	mainFile.addEnvironment("client", "server")
}
