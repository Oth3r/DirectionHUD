plugins {
	id 'base'
	id 'fabric-loom' version '1.11-SNAPSHOT'
	id 'maven-publish'
	id "me.modmuss50.mod-publish-plugin" version "0.8.4"
}

base {
	archivesName = project.file_name_fabric
}
version = project.mod_version
group = project.maven_group

repositories {
	mavenLocal()
	maven {
		name = "Terraformers"
		url = "https://maven.terraformersmc.com/"
	}
	maven {
		name = "geyser"
		url = uri("https://repo.opencollab.dev/main/")
	}
	maven {
		name = "DevAuth"
		url = "https://pkgs.dev.azure.com/djtheredstoner/DevAuth/_packaging/public/maven/v1"
	}
	maven {
		name "oth3rMavenSnapshots"
		url "https://maven.oth3r.one/snapshots"
	}
}

sourceSets {
	// Define a new sourceSet for the common code
	main {
		java {
			srcDir '../common/src/main/java'
		}
		resources {
			srcDir '../common/src/main/resources'
		}
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"

	include implementation('org.apache.commons:commons-text:1.10.0')
	modRuntimeOnly "me.djtheredstoner:DevAuth-fabric:${project.devauth_version}"

	// otterlib
	modImplementation "one.oth3r:otterlib:${project.otterlib_version}-fabric"
}

processResources {
	filteringCharset = "UTF-8"

	var replaceProperties = [
			version                : project.version,
			minecraft_version      : minecraft_version,
			min_minecraft_version  : min_minecraft_version,
			max_minecraft_version  : max_minecraft_version,
			otterlib_version       : otterlib_version,
			otterlib_max_version   : otterlib_max_version,
			loader_version         : loader_version
	]

	inputs.properties replaceProperties
	filesMatching("fabric.mod.json") {
		expand replaceProperties
	}
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}

publishMods {
	changelog = file('../changelog.md').text
	type = STABLE

	file = remapJar.archiveFile
	modLoaders.add("fabric")
	modLoaders.add("quilt")

	version = mod_version
	displayName = "v${mod_version} [Fabric]"

	curseforge {
		accessToken = env.fetchOrNull('CURSEFORGE')
		projectId = "843483"
		projectSlug = "directionhud-fabric"
		minecraftVersions.addAll(minecraft_versions.split(",").toList())

		requires("fabric-api", "otterlib")

		announcementTitle = "Download from CurseForge"
	}

	modrinth {
		accessToken = env.fetchOrNull('MODRINTH')
		projectId = "H8WCXRtb"
		minecraftVersions.addAll(minecraft_versions.split(",").toList())

		requires("fabric-api", "otterlib")

		announcementTitle = "Download from Modrinth"
	}
}